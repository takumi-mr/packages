# Maintainer: graysky <graysky@archlnux.us>
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Ray Sherwin <sliick517d@gmail.com>

pkgname=firmware-raspberrypi-pikvm
_firmcommit=fdaf74c780ca7a29b12d62e5b0d37c38c2321e20
_bluezcommit=e7fd166981ab4bb9a36c2d1500205a078a35714d
pkgver=20220411
pkgrel=1
pkgdesc="Additional firmware for Raspberry Pi"
arch=('any')
url="https://github.com/RPi-Distro/firmware-nonfree/commits/master/brcm"
license=('custom')
makedepends=('git')
options=('!strip')
source=("git+https://github.com/RPi-Distro/firmware-nonfree.git#commit=$_firmcommit"
        # support for which device?
        "BCM4345C5.hcd::https://github.com/RPi-Distro/bluez-firmware/raw/$_bluezcommit/broadcom/BCM4345C5.hcd"
        # bluetooth for RPi3+
        "BCM43430A1.hcd::https://github.com/RPi-Distro/bluez-firmware/raw/$_bluezcommit/broadcom/BCM43430A1.hcd"
        "BCM4345C0.hcd::https://github.com/RPi-Distro/bluez-firmware/raw/$_bluezcommit/broadcom/BCM4345C0.hcd"
        # bluetooth for Zero 2 W
        "BCM43430B0.hcd::https://github.com/RPi-Distro/bluez-firmware/raw/$_bluezcommit/broadcom/BCM43430B0.hcd"
        # fixes buggy linux-firmware for RPi4B
        'brcmfmac43455-sdio.raspberrypi,4-model-b.txt'
        # fixes buggy linux-firmware for Zero-W
        'https://archlinuxarm.org/builder/src/bcm43430/7.45.98.38/brcmfmac43430-sdio.txt'
        'RPi-WM8804.conf')

sha256sums=('SKIP'
            'dde785c4fa1351b52bda9c74554ae55bc74ecbe44935b9c3d83c7fe282cf17cf'
            'c096ad4a5c3f06ed7d69eba246bf89ada9acba64a5b6f51b1e9c12f99bb1e1a7'
            'c723c903655086c9f3a1de94a291f495a0f087f1ee6d3ee7bc4c558497aae7bc'
            '338c2c6631131f516bfc7e64ef0872bd0402e1f98ef9d0c900eef0c814d90a25'
            'edb6f4e4fb19e18940004124feb4ffe160d72fc607243a07a4480338a28b2748'
            'e4ce7ad5ce72406f958ed758539b65d958d1c0eb3e46e3f8e9f02b510d88e0b0'
            'f978fbc40db75ba3213a4472023496d0716706eb1a6f078f207ac027c5753f43')

conflicts=(firmware-raspberrypi)
provides=("firmware-raspberrypi=$pkgver")

package() {
  install -d "$pkgdir"/usr/lib/firmware/updates/brcm "$pkgdir"/usr/share/alsa/cards/
  install -m 0644 *.hcd "$pkgdir"/usr/lib/firmware/updates/brcm
  install -m 0644 RPi-WM8804.conf "$pkgdir"/usr/share/alsa/cards/
  install -m 0644 "$srcdir"/firmware-nonfree/debian/config/brcm80211/brcm/* "$pkgdir"/usr/lib/firmware/updates/brcm
  rm "$pkgdir"/usr/lib/firmware/updates/brcm/{brcmfmac43430-sdio.bin,brcmfmac43430-sdio.txt}

  # make some symbolic links
  cd "$pkgdir"/usr/lib/firmware/updates/brcm
  ln -s ./brcmfmac43456-sdio.txt ./brcmfmac43456-sdio.raspberrypi,400.txt
  ln -s ./brcmfmac43456-sdio.txt ./brcmfmac43456-sdio.raspberrypi,4-compute-module.txt
  ln -s ./brcmfmac43456-sdio.txt ./brcmfmac43456-sdio.pine64-pinephone-pro.txt
  ln -s ./brcmfmac43455-sdio.raspberrypi,4-model-b.txt ./brcmfmac43455-sdio.raspberrypi,4-compute-module.txt
  ln -s ./BCM43430B0.hcd ./BCM.hcd
}
